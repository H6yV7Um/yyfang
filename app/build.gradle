apply plugin: 'com.android.application'
apply plugin: 'realm-android'


// 得到当前时间的字符串 用于发包的时候拼接命名
// 关于语言的实现参考 groovy def定义无类型的变量 返回值无类型的方法
def static releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.yiyanf.fang"
        minSdkVersion 21
        targetSdkVersion 26
        // dex突破65535的限制
        multiDexEnabled true
        versionCode 30
        versionName "1.3.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            //选择要添加的对应cpu类型的.so库。
            abiFilters 'armeabi', 'armeabi-v7a'
            // 还可以添加 'x86', 'x86_64', 'mips', 'mips64'
        }

        // 极光推送相关配置
        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "9c65f92724b24f36df24159e", //JPush上注册的包名对应的appkey.
                JPUSH_CHANNEL: "yiyanf"
        ]

        // 解决studio3.0 上Butterknife报错问题
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }
    signingConfigs {
        debug {
            storeFile file('rjzd.release.keystore')
            storePassword rootProject.properties['RELEASE_STORE_PASSWORD']
            keyAlias rootProject.properties['RELEASE_KEY_ALIAS']
            keyPassword rootProject.properties['RELEASE_KEY_PASSWORD']
        }

        release {
            storeFile file('rjzd.release.keystore')
            storePassword rootProject.properties['RELEASE_STORE_PASSWORD']
            keyAlias rootProject.properties['RELEASE_KEY_ALIAS']
            keyPassword rootProject.properties['RELEASE_KEY_PASSWORD']
            v2SigningEnabled false
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        release {
            //  release版去除多余的资源，减少apk的体积，俩属性必须都为true
            /*minifyEnabled true
            shrinkResources true*/
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    //自动生成指定名称的release发布版的 apk文件
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "yiyanfang_${buildType.name}_${defaultConfig.versionName}_${releaseTime()}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    //Butterknife 依赖
    compile 'com.jakewharton:butterknife:7.0.1'
    //RxJava 依赖
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'
    //Retrofit2.0 所需要的包
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    //Retrofit2 ConverterFactory的Gson依赖包
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    // Glide 加载图片框架
    compile 'com.github.bumptech.glide:glide:3.7.0'
    // 友盟统计依赖
    compile 'com.umeng.sdk:common:1.5.0'
    compile 'com.umeng.sdk:analytics:7.5.0'

    // 弹幕依赖
    compile 'com.github.ctiao:dfm:0.4.4'
    // 腾讯小视频依赖
    compile(name: 'LiteAVSDK_Professional', ext: 'aar')
    // 腾讯云依赖
    compile(name: 'presentation', ext: 'aar')
    // 解决dex突破65535时候，项目报错
    compile 'com.android.support:multidex:1.0.2'
    // eventBus依赖
    compile 'org.greenrobot:eventbus:3.0.0'
    // 极光推送添加依赖
    compile 'cn.jiguang.sdk:jpush:3.1.1'
    compile 'cn.jiguang.sdk:jcore:1.1.9'

}
